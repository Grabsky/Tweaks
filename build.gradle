plugins {
    id("java-library")
    id("com.gradleup.shadow") version "8.3.5"
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("de.eldoria.plugin-yml.paper") version "0.7.1"
}

final String VERSION = "1.21.7"
final String RUN_NUMBER = System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"

group = "cloud.grabsky"
version = "$VERSION-$RUN_NUMBER"

repositories {
    mavenLocal()
    mavenCentral()
    // Repository containing the 'Paper API' dependency.
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    // Repository containing the 'PacketEvents' dependency.
    maven { url = "https://repo.codemc.io/repository/maven-public" }
    // Repository containing the 'PlaceholderAPI' dependency.
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    // Repository containing all my libraries.
    maven { url = "https://vela.grabsky.workers.dev/releases" }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")
    // Paper (Provided)
    compileOnly("io.papermc.paper:paper-api:1.21.7-R0.1-SNAPSHOT")
    // PacketEvents (Provided)
    compileOnly("com.github.retrooper:packetevents-spigot:2.9.1")
    // PlaceholderAPI (Provided)
    compileOnly("me.clip:placeholderapi:2.11.6")
    // Azure (Provided)
    compileOnly("cloud.grabsky:azure-api:1.21.7-142") {
        exclude group: "cloud.grabsky", module: "bedrock"
    }
    // Dependencies (My Libraries) (Shaded)
    paperLibrary("cloud.grabsky:bedrock:1.21-67")
    paperLibrary("cloud.grabsky:commands:1.20.1-40")
    paperLibrary("cloud.grabsky:configuration-paper:1.21.4-54")
    // Dependencies (Other Libraries) (Shaded)
    paperLibrary("com.jeff-media:MorePersistentDataTypes:2.4.0")
}

tasks {
    runServer {
        minecraftVersion("1.21.7")
        jvmArgs("--enable-preview")
        // Declaring plugin dependencies. These will be downloaded automatically.
        downloadPlugins {
            modrinth("packetevents", "2.9.1+spigot")
            modrinth("luckperms", "v5.5.0-bukkit")
            hangar("PlaceholderAPI", "2.11.6")
            github("Grabsky", "Azure", "1.21.7+142", "Azure-1.21.7-142.jar")
        }

    }
    shadowJar {
        shadowJar.archiveFileName = "${rootProject.name}-${project.version}.jar"
    }
    compileJava {
        options.fork = true
        options.compilerArgs += "--enable-preview"
        // Setting compatibility to Java 21 (above should work too).
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

paper {
    main = "cloud.grabsky.tweaks.Tweaks"
    loader = "cloud.grabsky.tweaks.Tweaks\$PluginLoader"
    apiVersion = "1.21.7"
    generateLibrariesJson = true
    serverDependencies {
        "Azure"          { load = "BEFORE"; required = true }
        "packetevents"   { load = "BEFORE"; required = true }
        "PlaceholderAPI" { load = "BEFORE"; required = false }
    }
}


// Returns formatted release name. (for GitHub Actions)
tasks.register("getRelease", () -> print("$VERSION #$RUN_NUMBER"))

// Returns formatted tag name. (for GitHub Actions)
tasks.register("getTag", () -> print("$VERSION+$RUN_NUMBER"))
