plugins {
    id("java-library")
    id("com.gradleup.shadow") version "8.3.5"
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("de.eldoria.plugin-yml.paper") version "0.7.1"
}

group = "cloud.grabsky"
version = "1.21.5-${System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"}"

repositories {
    mavenLocal()
    mavenCentral()
    // Repository containing the 'Paper API' dependency.
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    // Repository containing the 'PacketEvents' and 'Item-NBT-API' dependencies.
    maven { url = "https://repo.codemc.io/repository/maven-public" }
    // Repository containing the 'PlaceholderAPI' dependency.
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    // Repository containing the 'bedrock' dependency.
    maven { url = "https://maven.pkg.github.com/Grabsky/bedrock"; fillCredentials(it) }
    // Repository containing the 'configuration' dependency.
    maven { url = "https://maven.pkg.github.com/Grabsky/configuration"; fillCredentials(it) }
    // Repository containing the 'commands' dependency.
    maven { url = "https://maven.pkg.github.com/Grabsky/commands"; fillCredentials(it) }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")
    // Paper (Provided)
    compileOnly("io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT")
    // PacketEvents (Provided)
    compileOnly("com.github.retrooper:packetevents-spigot:2.7.0")
    // PlaceholderAPI (Provided)
    compileOnly("me.clip:placeholderapi:2.11.6")
    // Azure (Provided)
    compileOnly("cloud.grabsky:azure-api:1.21.4-109") {
        exclude group: "cloud.grabsky", module: "bedrock"
    }
    // Dependencies (My Libraries) (Shaded)
    implementation("cloud.grabsky:bedrock:1.21-64")
    implementation("cloud.grabsky:commands:1.20.1-37")
    implementation("cloud.grabsky:configuration-paper:1.21.4-51")
    // Dependencies (Other Libraries) (Shaded)
    implementation("com.jeff-media:MorePersistentDataTypes:2.4.0")
}

tasks {
    runServer {
        minecraftVersion("1.21.5")
        jvmArgs("--enable-preview")
    }
    shadowJar {
        shadowJar.archiveFileName = "${rootProject.name}-${project.version}.jar"
    }
    compileJava {
        options.fork = true
        options.compilerArgs += "--enable-preview"
        // Setting compatibility to Java 21 (above should work too).
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

paper {
    main = "cloud.grabsky.tweaks.Tweaks"
    apiVersion = "1.21.5"
    serverDependencies {
        "Azure"          { load = "BEFORE"; required = true }
        "packetevents"   { load = "BEFORE"; required = true }
        "PlaceholderAPI" { load = "BEFORE"; required = false }
    }
}


// Returns formatted release name. (for GitHub Actions)
tasks.register("getRelease", () -> print("$VERSION #$RUN_NUMBER"))

// Returns formatted tag name. (for GitHub Actions)
tasks.register("getTag", () -> print("$VERSION+$RUN_NUMBER"))


/* UTILITY METHODS */

private final def fillCredentials(final MavenArtifactRepository repository) {
    repository.credentials {
        username = findProperty("gpr.actor") ?: System.getenv("GITHUB_ACTOR")
        password = findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
    }
}
