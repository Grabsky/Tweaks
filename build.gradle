plugins {
    id("java-library")
    id("net.minecrell.plugin-yml.paper") version "0.6.0"
    id("com.gradleup.shadow") version "8.3.5"
}

group = "cloud.grabsky"
version = "1.21.4-${System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"}"

repositories {
    mavenLocal()
    mavenCentral()
    // Repository for the Paper API. (Forked; contains VaultStateChangeEvent)
    maven { url = "https://maven.pkg.github.com/grabsky/paper"; fillCredentials(it) }
    // Repository for the Paper API.
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    // Repository containing the 'PacketEvents' and 'Item-NBT-API' dependencies.
    maven { url = "https://repo.codemc.io/repository/maven-public" }
    // Repository containing the 'PlaceholderAPI' dependency.
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    // Repository containing the 'bedrock' dependency.
    maven { url = "https://maven.pkg.github.com/grabsky/bedrock"; fillCredentials(it) }
    // Repository containing the 'configuration' dependency.
    maven { url = "https://maven.pkg.github.com/grabsky/configuration"; fillCredentials(it) }
    // Repository containing the 'commands' dependency.
    maven { url = "https://maven.pkg.github.com/grabsky/commands"; fillCredentials(it) }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")
    // Paper (Forked; contains VaultStateChangeEvent)
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    // PacketEvents (Provided)
    compileOnly("com.github.retrooper:packetevents-spigot:2.6.0")
    // PlaceholderAPI (Provided)
    compileOnly("me.clip:placeholderapi:2.11.6")
    // Dependencies (My Libraries) (Shaded)
    implementation("cloud.grabsky:bedrock:1.21-63")
    implementation("cloud.grabsky:commands:1.20.1-37")
    implementation("cloud.grabsky:configuration-paper:1.21.3-45")
    // Dependencies (Other Libraries) (Shaded)
    implementation("com.jeff-media:MorePersistentDataTypes:2.4.0")
    implementation("de.tr7zw:item-nbt-api:2.14.1-SNAPSHOT")
}

tasks {
    shadowJar { shadowJar.archiveFileName = "${rootProject.name}-${project.version}.jar" }
    compileJava {
        options.fork = true
        options.compilerArgs += "--enable-preview"
        // Setting compatibility to Java 21 (above should work too).
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

paper {
    main = "cloud.grabsky.tweaks.Tweaks"
    apiVersion = "1.21.4"
    serverDependencies {
        "Azure"          { load = "BEFORE"; required = true }
        "packetevents"   { load = "BEFORE"; required = true }
        "PlaceholderAPI" { load = "BEFORE"; required = false }
    }
}

/* FOR GITHUB ACTIONS */

tasks.register("release") {
    doLast { print("${version.toString().split('-')[0]} #${System.getenv("GITHUB_RUN_NUMBER")}") }
}

tasks.register("tag") {
    doLast { print(version) }
}

/* UTILITY METHODS */

private final def fillCredentials(final MavenArtifactRepository repository) {
    repository.credentials {
        username = findProperty("gpr.actor") ?: System.getenv("GITHUB_ACTOR")
        password = findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
    }
}
